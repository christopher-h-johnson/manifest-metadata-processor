buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'org.javamodularity.moduleplugin' version '1.8.10' apply false
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

ext {
    /* Dependencies */
    activationApiVersion = '1.1.1'
    camelVersion = '2.25.4'
    commonsIoVersion = '2.6'
    commonsRdfVersion = '0.5.0'
    elasticsearchVersion = '6.2.2'
    imageMetadataVersion = '0.2.0-SNAPSHOT'
    iiifVocabularyVersion = '0.1.1'
    jacksonVersion = '2.9.5'
    jaxbVersion = '2.3.1'
    jsonldVersion = '0.12.0'
    leptonicaVersion = '1.75.3-1.4.1'
    logbackVersion = '1.2.10'
    slf4jVersion = '1.7.32'
    tesseractVersion = '3.05.01-1.4.1'
    trellisClientVersion = '0.3.0-SNAPSHOT'
    xbeamVersion = '1.4.16'

    /* Testing */
    httpClientOsgiVersion = '4.5.3'
    jenaVersion = '3.7.0'
    junitVersion = "5.8.2"
    xercesVersion = '2.11.0'
    mockitoVersion = '4.2.0'
    slf4jVersion = '1.7.32'

    /* Transitive patched modules */
    commonsLoggingVersion = '1.2'
    jaxbCoreVersion = '2.2.11'
    httpClientOsgiVersion = '4.5.3'
    httpCoreOsgiVersion = '4.4.6'

    jacocoVersion = '0.8.7'
    checkstyleVersion = '8.8'
}

configure(allprojects) { project ->

    ext {
        name = 'Manifest Metadata Processor API'
        description = 'A Camel LDP implementation'
        vendor = 'pandorasystems'
        homepage = 'http://www.trellisldp.org'
        url = 'http://www.trellisldp.org/apidocs'
        license = 'Apache 2'
        inceptionYear = '2018'
    }

    group = 'de.ubleipzig'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

subprojects { subproj ->
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'org.javamodularity.moduleplugin'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }


    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleVersion
    }

    license {
        include "**/*.java"
        //noinspection GroovyAssignabilityCheck
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        //noinspection GroovyAssignabilityCheck
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    task processConfig(type: Copy) {
        from('src/main/cfg') {
            include '**/*.cfg'
        }
        into 'build/cfg/main'
    }

    classes {
        classes.dependsOn processConfig
    }

    dependencies {
        testImplementation(platform("org.junit:junit-bom:$junitVersion"))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()

        systemProperty 'java.util.logging.config.file', "${subproj.buildDir}/resources/test/logging-test.properties"

        forkEvery = 1

        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
            showStandardStreams = true
        }
    }
}

configure(rootProject) {
    license {
        include "**/*.java"
        exclude "**/*Test.java"
        //noinspection GroovyAssignabilityCheck
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        //noinspection GroovyAssignabilityCheck
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }
}
